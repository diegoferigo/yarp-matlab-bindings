# As matlab support is higly experimental for now in swig, we split the binding generation in two phases:
#  * Generation of the .cxx code, left to the author of the library that needs to have a recent (non-standard) swig
#  * Compilation of the .cxx, that is left to the user that compiles the library.
# For doing this we split the traditional swig_add_module macro in two macro: one for generating the wrapper and one
# for compiling it. As soon as upstream swig distributed by the distro gains Matlab support, we can drop this workaround
macro(SWIG_GENERATE_MODULE name language)
  SWIG_MODULE_INITIALIZE(${name} ${language})
  set(swig_dot_i_sources)
  set(swig_other_sources)
  foreach(it ${ARGN})
    if(${it} MATCHES ".*\\.i$")
      set(swig_dot_i_sources ${swig_dot_i_sources} "${it}")
    else()
      set(swig_other_sources ${swig_other_sources} "${it}")
    endif()
  endforeach()

  set(swig_generated_sources)
  foreach(it ${swig_dot_i_sources})
    SWIG_ADD_SOURCE_TO_MODULE(${name} swig_generated_source ${it})
    set(swig_generated_sources ${swig_generated_sources} "${swig_generated_source}")
  endforeach()
endmacro()

macro(SWIG_COMPILE_MODULE name language)
  SWIG_MODULE_INITIALIZE(${name} ${language})
  add_library(${name}
          MODULE
          ${swig_generated_sources}
          ${swig_other_sources})
endmacro()

# Options related to installation directories
# If you want to install bindings for packaging you may need to install
# following several rules (for example placing the .m files in share
# and the compiled libraries in some architecture-specific directory)
# We support this use-cases by exposing this CMake advanced option.
# The default values, however, are choosen to simplify the use of
# the bindings for a developer that compiled the library from source:
# to use the octave bindings just add <prefix>/octave to the octave path,
# to use the matlab bindings just add <prefix>/mex    to the matlab path.
set(YARP_INSTALL_MATLAB_LIBDIR "mex" CACHE STRING "Location (relative to the install prefix) in which the Matlab mex libraries are installed.")
mark_as_advanced(YARP_INSTALL_MATLAB_LIBDIR)
set(YARP_INSTALL_MATLAB_MFILESDIR "mex" CACHE STRING "Location (relative to the install prefix) in which the Matlab .m files are installed.")
mark_as_advanced(YARP_INSTALL_MATLAB_MFILESDIR)
set(YARP_INSTALL_OCTAVE_LIBDIR "octave" CACHE STRING "Location (relative to the install prefix) in which the Octave mex libraries are installed.")
mark_as_advanced(YARP_INSTALL_OCTAVE_LIBDIR)
set(YARP_INSTALL_OCTAVE_MFILESDIR "octave" CACHE STRING "Location (relative to the install prefix) in which the Octave .m files are installed.")
mark_as_advanced(YARP_INSTALL_OCTAVE_MFILESDIR)

# The name of the generated source named is
# defined by the variable swig_generated_file_fullname
# in the SWIG_ADD_SOURCE_TO_MODULE macro in UseSWIG.cmake
set(sourcename yarpMATLAB_wrap)

# The name of the generated source should instead match the one
# used by SWIG when generating the .m file.
# See https://github.com/jaeandersson/swig/issues/44 for more details
set(mexname    yarpMEX)

# Directory in which the bindings are generated
set(MEX_BINDINGS_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/autogenerated)

## ==================

if(${CMAKE_VERSION} VERSION_GREATER 3.8)
  macro(GET_LIBRARY_FROM_SWIG_WRAPPER file name generate outputdir)

    set_property(SOURCE ${file} PROPERTY CPLUSPLUS ON)
    set(CMAKE_SWIG_OUTDIR ${outputdir})

    if(${generate} AND NOT bindings_have_been_generated)
      find_package(SWIG)
      set(bindings_have_been_generated TRUE)
      swig_add_library(${name} LANGUAGE matlab SOURCES ${YARP_SWIG_I_FILE})
    else()
      swig_add_library(${name} LANGUAGE matlab NO_PROXY SOURCES ${YARP_SWIG_I_FILE})
    endif()
    set_property(TARGET ${name} PROPERTY UseSWIG_TARGET_NAME_PREFERENCE LEGACY)
    set(TARGET_NAME ${SWIG_MODULE_${name}_REAL_NAME})
  endmacro()
else()
  # Generate SWIG wrapper
  if(YARP_GENERATE_MATLAB)
    find_package(SWIG)
    # generate the wrapper
    set(swig_generated_sources)
    set_source_files_properties(${YARP_SWIG_I_FILE} PROPERTIES CPLUSPLUS ON)
    # generate files in the source directory, so we can commit it
    set(CMAKE_SWIG_OUTDIR ${MEX_BINDINGS_SOURCE_DIR})
    #set(CMAKE_SWIG_FLAGS "-redirectoutput")
    set(SWIG_MODULE_${mexname}_EXTRA_DEPS ${YARP_SWIG_DEPENDS_I_FILES})
    swig_generate_module(${mexname} matlab ${YARP_SWIG_I_FILE})
    set_source_files_properties(${MEX_BINDINGS_SOURCE_DIR}/${sourcename}.cxx PROPERTIES GENERATED 1)
  endif()
endif()

# ==============
# COMPILE MATLAB
# ==============

# Set the generated mex name to be yarpMEX, as it the defaul one used by SWIG while generating bindings
if(YARP_USES_MATLAB)
  find_package(Matlab
          REQUIRED
          MX_LIBRARY
          MAIN_PROGRAM)

  if(${CMAKE_VERSION} VERSION_GREATER 3.8)
    get_library_from_swig_wrapper(
        ${YARP_SWIG_I_FILE}
        yarpMatlabMex
        ${YARP_GENERATE_MATLAB}
        ${MEX_BINDINGS_SOURCE_DIR})
  else()
    # use previously generated files
    set(swig_generated_sources ${MEX_BINDINGS_SOURCE_DIR}/${sourcename}.cxx)
    set(swig_other_sources)
    # Compile the wrapper
    swig_compile_module(yarpMatlabMex matlab)
  endif()

  target_link_libraries(yarpMatlabMex ${Matlab_LIBRARIES} ${YARP_LIBRARIES})
  target_include_directories(yarpMatlabMex PUBLIC ${Matlab_INCLUDE_DIRS})

  set_target_properties(yarpMatlabMex PROPERTIES PREFIX "" SUFFIX .${Matlab_MEX_EXTENSION})
  # entry point in the mex file + taking care of visibility and symbol clashes.
  if(WIN32)
    set_target_properties(yarpMatlabMex
            PROPERTIES
            DEFINE_SYMBOL "DLL_EXPORT_SYM=__declspec(dllexport)")
  endif()

  # Install the generated front-end to ${CMAKE_INSTALL_PREFIX}/mex
  install(
      TARGETS yarpMatlabMex
      EXPORT yarp-matlab-bindings
      LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
      ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
      RUNTIME DESTINATION ${CMAKE_INSTALL_LIBDIR}
      DESTINATION ${YARP_INSTALL_OCTAVE_LIBDIR})
  list(APPEND EXPORTED_TARGETS yarpMatlabMex)

  install(DIRECTORY ${MEX_BINDINGS_SOURCE_DIR}/+yarp DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_MATLAB_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigRef.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_MATLAB_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigMem.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_MATLAB_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigGet.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_MATLAB_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigStorage.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_MATLAB_MFILESDIR})
  install(TARGETS yarpMatlabMex DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_MATLAB_LIBDIR})

  #On new versions of Clang, MATLAB requires C++11.
  #I enable it on all Clangs
  if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
    if(${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
      set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
      if (${CMAKE_GENERATOR} MATCHES "Xcode")
        #this should set explictly the option in xcode. Unfortunately it does not work.
        set(XCODE_ATTRIBUTE_CLANG_CXX_LANGUAGE_STANDARD "C++11")
      endif(${CMAKE_GENERATOR} MATCHES "Xcode")
    endif(${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
  endif(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
endif()

# ==============
# COMPILE OCTAVE
# ==============

if(YARP_USES_OCTAVE)
  find_package(Octave REQUIRED)
  if ("${OCTAVE_VERSION_STRING}" VERSION_LESS 4.0)
    message(FATAL_ERROR "Octave mex-based bindings required at least Octave version 4.0 or greater.")
  endif ()

  if(${CMAKE_VERSION} VERSION_GREATER 3.8)
    get_library_from_swig_wrapper(
        ${YARP_SWIG_I_FILE}
        yarpOctaveMex
        ${YARP_GENERATE_MATLAB}
        ${MEX_BINDINGS_SOURCE_DIR})
  else()
      # use previously generated files
      set(swig_generated_sources ${MEX_BINDINGS_SOURCE_DIR}/${sourcename}.cxx)
      set(swig_other_sources)
      # Compile the wrapper
      swig_compile_module(yarpOctaveMex matlab)
  endif()

  # Compile MEX file
  target_include_directories(yarpOctaveMex PUBLIC ${OCTAVE_INCLUDE_DIRS})
  if(APPLE)
    target_link_libraries(yarpOctaveMex ${YARP_LIBRARIES})
    set_target_properties(yarpOctaveMex PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
  else()
    target_link_libraries(yarpOctaveMex ${OCTAVE_LIBRARIES} ${YARP_LIBRARIES})
  endif()
  set_target_properties(yarpOctaveMex
          PROPERTIES OUTPUT_NAME "yarpMEX"
          PREFIX ""
          SUFFIX .mex)
  install(
      TARGETS yarpOctaveMex
      EXPORT yarp-matlab-bindings
      LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
      ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
      RUNTIME DESTINATION ${CMAKE_INSTALL_LIBDIR}
      DESTINATION ${YARP_INSTALL_OCTAVE_LIBDIR})
  list(APPEND EXPORTED_TARGETS yarpOctaveMex)

  # Install the generated front-end to ${CMAKE_INSTALL_PREFIX}/mex
  install(DIRECTORY ${MEX_BINDINGS_SOURCE_DIR}/+yarp DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_OCTAVE_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigRef.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_OCTAVE_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigMem.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_OCTAVE_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigGet.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_OCTAVE_MFILESDIR})
  install(FILES ${MEX_BINDINGS_SOURCE_DIR}/SwigStorage.m DESTINATION ${CMAKE_INSTALL_PREFIX}/${YARP_INSTALL_OCTAVE_MFILESDIR})
endif()

if((TARGET yarpOctaveMex OR TARGET yarpMatlabMex) AND YCM_FOUND)
  include(InstallBasicPackageFiles)
  install_basic_package_files(yarp-matlab-bindings
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
    TARGETS ${EXPORTED_TARGETS}
    NO_CHECK_REQUIRED_COMPONENTS_MACRO)
endif()
